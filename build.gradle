apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'nexus'

sourceCompatibility = 1.5
targetCompatibility = 1.5
group = 'org.gradle.api.plugins'
version = '0.5'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.2'
    }
}

ext.codeNarcConfigFileName = 'config/codenarc/rules.groovy'

repositories {
    mavenCentral()
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile 'org.gradle.api.plugins:gradle-gae-plugin:0.7.6'

    testCompile "org.spockframework:spock-core:0.7-groovy-1.8",
                "junit:junit:4.10"
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Gaelyk plugin',
                   'Implementation-Version': version,
                   'Built-By': System.getProperty("user.name"),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty("java.version"),
                   'Built-Gradle': gradle.gradleVersion
    }
}

idea {
    project {
        jdkName = '1.6'
  
        ipr.withXml { provider ->
            def node = provider.asNode()

            // Use GIT
            def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
            vcsConfig.mapping[0].'@vcs' = 'Git'

            // Set Gradle home
            def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
            gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir])
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.7'
}

modifyPom {
    project {
        name 'Gradle Gaelyk plugin'
        description 'Gradle plugin that provides tasks for managing Gaelyk projects.'
        url 'https://github.com/bmuschko/gradle-gaelyk-plugin'
        inceptionYear '2011'

        scm {
            url 'https://github.com/bmuschko/gradle-gaelyk-plugin'
            connection 'scm:https://bmuschko@github.com/bmuschko/gradle-gaelyk-plugin.git'
            developerConnection 'scm:git://github.com/bmuschko/gradle-gaelyk-plugin.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'bmuschko'
                name 'Benjamin Muschko'
                email 'benjamin.muschko@gmail.com'
            }
            developer {
                id 'vladimirorany'
                name 'Vladimir Orany'
                email 'vladimir@orany.cz'
            }
            developer {
                id 'codeconsole'
                name 'Scott Murphy'
                email 'scott@alwaysvip.com'
            }
            developer {
                id 'erdi'
                name 'Marcin Erdmann'
            }
        }
    }
}
