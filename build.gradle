buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7'
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.0"
    }
}


apply plugin: 'groovy'
 apply plugin: 'nexus'
apply plugin: "com.gradle.plugin-publish"

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'org.gradle.api.plugins'
version = '0.7.1'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    def localGroovy = localGroovy()
    compile localGroovy
    compile gradleApi()

    compile 'com.google.appengine:gradle-appengine-plugin:1.9.34'
    compile 'io.github.kdabir.directree:directree:0.2', {
        exclude group:'org.codehaus.groovy'
    }

    File localGroovyJar     =  localGroovy.resolve().asList().first()
    String spockQualifier   = '2.0'

    if (localGroovyJar.name.contains('-1.8')) {
        spockQualifier = '1.8'
    }
    
    testCompile "org.spockframework:spock-core:0.7-groovy-${spockQualifier}", {
        exclude group:'org.codehaus.groovy'
    }
    testCompile "junit:junit:4.10"
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Gaelyk plugin',
                   'Implementation-Version': version,
                   'Built-By': System.getProperty("user.name"),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty("java.version"),
                   'Built-Gradle': gradle.gradleVersion
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

modifyPom {
    project {
        name 'Gradle Gaelyk plugin'
        description 'Gradle plugin that provides tasks for managing Gaelyk projects.'
        url 'https://github.com/gaelyk/gradle-gaelyk-plugin'
        inceptionYear '2011'

        scm {
            url 'https://github.com/gaelyk/gradle-gaelyk-plugin'
            connection 'scm:https://gaelyk@github.com/gaelyk/gradle-gaelyk-plugin.git'
            developerConnection 'scm:git://github.com/gaelyk/gradle-gaelyk-plugin.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'bmuschko'
                name 'Benjamin Muschko'
                email 'benjamin.muschko@gmail.com'
            }
            developer {
                id 'vladimirorany'
                name 'Vladimir Orany'
                email 'vladimir@orany.cz'
            }
            developer {
                id 'codeconsole'
                name 'Scott Murphy'
                email 'scott@alwaysvip.com'
            }
            developer {
                id 'erdi'
                name 'Marcin Erdmann'
            }
        }
    }
}

pluginBundle {
    website = 'https://github.com/gaelyk/gradle-gaelyk-plugin'
    vcsUrl = 'https://github.com/gaelyk/gradle-gaelyk-plugin'
    description = 'Gaelyk Plugin'
    tags = ['gaelyk', 'appengine', 'cloud', 'gae']

    plugins {
        gaelykPlugin {
            id = 'org.gaelyk'
            displayName = 'Gaelyk Plugin'
        }
    }
}
