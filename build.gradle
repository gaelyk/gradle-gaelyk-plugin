apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'

sourceCompatibility = 1.5
targetCompatibility = 1.5
version = '0.3.2'
group = 'bmuschko'

ext.codeNarcConfigFileName = 'config/codenarc/rules.groovy'

repositories {
    mavenCentral()
    mavenRepo url: 'http://m2repo.spockframework.org/snapshots'
}

dependencies {
    groovy 'org.codehaus.groovy:groovy-all:1.8.6'
    
    compile gradleApi()
    compile 'com.jcraft:jsch:0.1.44-1'

    testCompile "org.spockframework:spock-core:0.6-groovy-1.8",
                "junit:junit:4.+"
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Gaelyk plugin',
                   'Implementation-Version': version,
                   'Built-By': System.getProperty("user.name"),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty("java.version")
    }
}

idea {
    project {
        jdkName = '1.6'
  
        ipr.withXml { provider ->
            def node = provider.asNode()

            // Use GIT
            def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
            vcsConfig.mapping[0].'@vcs' = 'Git'

            // Set Gradle home
            def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
            gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir])
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.0-milestone-9'
}
